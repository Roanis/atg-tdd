allprojects {
  version = '1.2.1'
}

ext {
  dynamo_home="$System.env.DYNAMO_HOME"
  m2Repo="$System.env.M2_REPO"
  atgJavadocLocation='http://docs.oracle.com/cd/E35319_01/Platform.10-2/apidoc'
}
logger.quiet "Build Environment for roanis/TDD"
logger.quiet "DYNAMO_HOME: ${dynamo_home}"


subprojects {
	apply plugin: 'java'
	apply plugin: 'maven'   
	apply plugin: 'eclipse'
		
	group = 'roanis.tdd'
	sourceCompatibility = 1.7
	
	repositories {   
		mavenLocal()
	  mavenCentral()
    // For any private maven repo ensure the properties below (mavenUrl, mavenUsername, mavenPassword) are added to
    // ~/.gradle/gradle.properties and uncomment the maven DSL below.
//    maven {
//      credentials {
//        username mavenUsername
//        password mavenPassword
//      }
//      url mavenUrl
//    }
	}  
  
  // Workaround for https://issues.gradle.org/browse/GRADLE-2812
  configurations.all {
      resolutionStrategy {
          force 'xml-apis:xml-apis:1.0.b2'
      }
  }    
	
	apply from: "$rootDir/gradle/dependencies.gradle"	
	
	dependencies {
	  compile (
      libraries.commons_collections,
	    libraries.google_guava,    
	    libraries.atg
    )
	  testCompile (
      libraries.junit,      
    )
    testRuntime(
      libraries.dust,
	    libraries.log4j,      
	    libraries.cglib,      
	    libraries.servlet_api,
      libraries.apache_ddlutils,
      libraries.hsqldb,
      libraries.jsp_api
    )
	}
	
	jar {	
	    manifest {
	        writeTo file(project.projectDir.getAbsolutePath() + '/META-INF/MANIFEST.MF')
	    }
	}	
	
	uploadArchives {
	    repositories {       
	        mavenDeployer {
	            repository(url: "file://" + m2Repo)
	        }
	    }
	}
	
	eclipse.classpath.file {
	    whenMerged { classpath ->
	    	def entries = classpath.entries.findAll { it instanceof org.gradle.plugins.ide.eclipse.model.Library  && it.path.indexOf('/lib/classes.jar') !=-1  }
	        entries.each { entry->
	        	entry.entryAttributes.put('javadoc_location', atgJavadocLocation)            
	        }        
	    }
	}
} 

apply from: "${rootDir}/gradle/tasks.gradle"

defaultTasks 'build'

task wrapper(type: Wrapper) {	
		gradleVersion = "2.0"
}